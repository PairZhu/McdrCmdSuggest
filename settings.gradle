pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
        mavenCentral()
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": 
					// 使用Fallen-Breath版本的preprocessor
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
            }
        }
    }
}

// 扫描versions目录下的所有子目录作为子项目
def versionsDir = file('versions')
versionsDir.listFiles().findAll {
    it.isDirectory() && it.name != 'mainProject'
}.each { versionDir ->
    def version = versionDir.name
    include(":$version")

    def proj = project(":$version")
    proj.projectDir = file("versions/$version")
    proj.buildFileName = "../../common.gradle"
}