pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
        mavenCentral()
        gradlePluginPortal()
    }
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case "com.replaymod.preprocess": 
					// 使用Fallen-Breath版本的preprocessor
                    useModule("com.github.Fallen-Breath:preprocessor:${requested.version}")
                    break
            }
        }
    }
}

// 扫描versions目录下的所有版本号子目录作为子项目
def versionsDir = file('versions')
versionsDir.listFiles().findAll { it.isDirectory() }.collect { it.name }
    .findAll { it.matches(/^\d+\.\d+(\.\d+)?$/) }  // 只匹配形如 X.Y 或 X.Y.Z 的版本号
    .each { version ->
    include(":$version")

    def proj = project(":$version")
    proj.projectDir = file("versions/$version")
    proj.buildFileName = "../../common.gradle"
}